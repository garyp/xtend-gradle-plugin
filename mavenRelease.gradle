apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
 
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required {gradle.taskGraph.hasTask("uploadArchives")}
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
 
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
 
            pom.project {
               name 'Xtend Gradle Plugin'
               packaging 'jar'
               description 'A Gradle Plugin for building Xtend projects'
               url 'https://github.com/oehme/xtend-gradle-plugin'
 
               scm {
                   url 'scm:git@github.com:oehme/xtend-gradle-plugin.git'
                   connection 'scm:git@github.com:oehme/xtend-gradle-plugin.git'
                   developerConnection 'scm:git@github.com:oehme/xtend-gradle-plugin.git'
               }
 
               licenses {
                   license {
                       name 'Eclipse Public License - v 1.0'
                       url 'http://www.eclipse.org/org/documents/epl-v10.php'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                       id 'oehme'
                       name 'Stefan Oehme'
                   }
               }
           }
        }
    }
}